plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.tratao'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [compileJava]*.options*.encoding = 'UTF-8'
}
compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [compileTestJava]*.options*.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.tratao:xcore:1.0.1'
    implementation 'com.alibaba:fastjson:2.0.21'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.hibernate.validator:hibernate-validator:6.0.13.Final'
    implementation 'org.glassfish:javax.el:3.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.5'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java

            // 添加 pom 相关信息
            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = project.name
                description = "xCurrency Hubs China Payout Java SDK"
                url = "https://github.com/TravelTao/xhub-java-sdk"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "xCurrency"
                        name = "dev"
                        email = "dev@tratao.com"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/TravelTao/xhub-java-sdk.git"
                    developerConnection = "scm:git:https://github.com/TravelTao/xhub-java-sdk.git"
                    url = "https://github.com/TravelTao/xhub-java-sdk"
                }
            }
        }
    }
    repositories {
        maven {
            name project.name
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}